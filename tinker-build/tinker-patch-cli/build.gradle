apply plugin: 'java-library'

version rootProject.ext.VERSION_NAME
group rootProject.ext.GROUP

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':tinker-build:tinker-patch-lib')
}

jar {
    manifest {
        attributes 'Main-Class': 'com.tencent.tinker.patch.CliMain'
        attributes 'Manifest-Version': version
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

// copy the jar to work directory
task buildTinkerSdk(type: Copy, dependsOn: [build, jar]) {
    group = "tinker"
    from('build/libs') {
        include '*.jar'
        exclude '*-javadoc.jar'
        exclude '*-sources.jar'
    }
    from('./tool_output') {
        include '*.*'
    }
    into(rootProject.file("buildSdk/build"))
}

defaultTasks 'buildTinkerSdk'
